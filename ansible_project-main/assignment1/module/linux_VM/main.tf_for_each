locals {
  instances = { for i in range(1, var.instance_count + 1) : i => "vmlinux-${i}" }
}

resource "azurerm_network_interface" "NI" {
  for_each = local.instances
  name                = "${var.linux_machine}-nic-${each.key}"
  location            = var.rg_location-9484
  resource_group_name = var.rg_name-9484
  
  ip_configuration {
    name                          = "${var.linux_machine}-ipconfig-${each.key}"
    subnet_id                     = var.subnet_id
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.pub_ip[each.key].id
  }
}

resource "azurerm_public_ip" "pub_ip" {
  for_each = local.instances
  name                = "${var.linux_machine}-pip-${each.key}"
  resource_group_name = var.rg_name-9484
  location            = var.rg_location-9484
  allocation_method   = "Dynamic"
  domain_name_label   = "${var.linux_machine}-${each.key}"

}

resource "azurerm_linux_virtual_machine" "vmlinux" {
  for_each = local.instances
  name                = "${var.linux_machine}-VM-${each.key}"
  computer_name = "${var.linux_machine}-VM-${each.key}"
  resource_group_name = var.rg_name-9484
  location            = var.rg_location-9484
  size                = var.size
  admin_username      = var.Admin_user
  availability_set_id   = azurerm_availability_set.as-9484.id
  network_interface_ids = [
    azurerm_network_interface.NI[each.key].id
  ]

  admin_ssh_key {
    username   = var.Admin_user
    public_key = file(var.pub_key)
  }

  source_image_reference {
    publisher = "OpenLogic"
    offer     = "CentOS"
    sku       = "8_2"
    version   = "latest"
  }

   os_disk {
    name = "${var.linux_machine}-osdisk-${each.key}"
    storage_account_type = "Standard_LRS"
    caching              = "ReadWrite"
    disk_size_gb         = "32"
  }
  boot_diagnostics {
    storage_account_uri = var.storage_account_uri
  }

}
resource "azurerm_availability_set" "as-9484" {
  name                         = var.linux_avs
  resource_group_name          = var.rg_name-9484
  location                     = var.rg_location-9484
  platform_fault_domain_count  = 2
  platform_update_domain_count = 5
}

resource "azurerm_virtual_machine_extension" "network_watcher" {
  for_each            = local.instances
  name                = "${var.linux_machine}-network_watcher-${each.key}"
  virtual_machine_id  = azurerm_linux_virtual_machine.vmlinux[each.key].id
  publisher           = "Microsoft.Azure.NetworkWatcher"
  type                = "NetworkWatcherAgentLinux"
  type_handler_version = "1.4"
}

resource "azurerm_virtual_machine_extension" "azure_monitor" {
  for_each            = local.instances
  name                = "${var.linux_machine}-azuremonitor-${each.key}"
  virtual_machine_id  = azurerm_linux_virtual_machine.vmlinux[each.key].id
  publisher           = "Microsoft.Azure.Monitor"
  type                = "AzureMonitorLinuxAgent"
  type_handler_version = "1.0"
  auto_upgrade_minor_version = true
}
